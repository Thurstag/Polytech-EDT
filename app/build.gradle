import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

def gitSha1 = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', "--short", 'origin/android-dev'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def gitMasterSha1 = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', "--short", 'origin/android-master'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def now() {
    return new SimpleDateFormat("yyyy/MM/dd - HH:mm:ss", Locale.FRANCE).format(new Date())
}

android {
    signingConfigs {
        releaseSign {
            storeFile file(project.file("keyStore.jks"))
            storePassword "polytech"
            keyAlias "polytechedt"
            keyPassword "polytech"
        }
    }
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.polytech.edt"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            buildConfigField "String", "BUILD_DATE", "\"${now()}\""
            buildConfigField "String", "GIT_COMMIT_SHA", "\"${gitSha1()}\""
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.releaseSign
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BUILD_DATE", "\"${now()}\""
            buildConfigField "String", "GIT_COMMIT_SHA", "\"${gitMasterSha1()}\""
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // DEPENDENCIES
    implementation 'org.apache.commons:commons-lang3:3.5'
    //noinspection DuplicatePlatformClasses
    implementation 'org.apache.httpcomponents:httpclient:4.5.6'
    implementation 'org.mnode.ical4j:ical4j:1.0.3'
    implementation 'com.github.alamkanak:android-week-view:1.2.6'
    implementation 'com.github.fge:jackson-coreutils:1.0'
    implementation 'commons-collections:commons-collections:3.0'

    // ANDROID DEPENDENCIES
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    //noinspection GradleCompatible
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //noinspection GradleCompatible
    implementation 'com.android.support:design:27.1.1'
    implementation 'androidx.annotation:annotation:1.1.0'

    // TEST DEPENDENCIES
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'
}
